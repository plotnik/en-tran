plugins {
    id 'groovy'
    id 'application'
}

mainClassName = 'io.plotnik.entran.Main'

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.groovy
    implementation libs.picocli
    implementation libs.sqlite
    implementation libs.tagsoup
    implementation libs.bundles.jackson
    implementation libs.slf4j

    testImplementation libs.junit.jupiter
    testImplementation libs.bundles.mockito
    testImplementation libs.assertj

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = mainClassName
}

tasks.named('test') {
    useJUnitPlatform()
}

/**
 * Compile README.adoc
 */
task readme(type: Exec) {
    commandLine 'asciidoctor', 'README.adoc'
}

/**
 * Compile fat jar with all dependencies included.
 */
task fatjar(type: Jar) {
    manifest {
        attributes "Main-Class": mainClassName
    }
    archiveBaseName = "en-tran-all"
    duplicatesStrategy = "include"
    from {
        configurations.compileClasspath.filter{ it.exists() }
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}
